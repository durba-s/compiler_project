START -> PROGRAM
PROGRAM -> DECL PROGRAM
PROGRAM -> FUNCTION PROGRAM
PROGRAM -> '' 
STMTS -> STMT STMTS
STMTS -> ''
CODE_BLOCK -> STMT
CODE_BLOCK -> SYM_P1 STMTS SYM_P2
STMT -> DECL
STMT -> ASSIGNMENT
STMT -> CONDITIONAL
STMT -> FOR
STMT -> WHILE
STMT -> DOWHILE
STMT -> JUMP
STMT -> SELECT
STMT -> EXPR SYM_SEMICOLON
DECL -> TYPE IDENTIFIER SYM_SEMICOLON
ASSIGNMENT -> IDENTIFIER ASGNEXT
ASGNEXT -> OP_ASSGN EXPR SYM_SEMICOLON
ASGNEXT -> SYM_SB1 DIM SYM_SB2 OP_ASSGN EXPR SYM_SEMICOLON
CONDITIONAL -> KEYWD_IF SYM_B1 EXPR SYM_B2 CODE_BLOCK KEYWD_ELSE CODE_BLOCK
FOR -> KEYWD_FOR IDENTIFIER KEYWD_IN SYM_B1 EXPR OP_COMMA EXPR SYM_B2 CODE_BLOCK
WHILE -> KEYWD_WHILE SYM_B1 EXPR SYM_B2 CODE_BLOCK
DOWHILE -> KEYWD_DO CODE_BLOCK KEYWD_WHILE SYM_B1 EXPR SYM_B2 SYM_SEMICOLON
JUMP -> KEYWD_RETURN EXPR SYM_SEMICOLON
JUMP -> KEYWD_BREAK SYM_SEMICOLON
JUMP -> KEYWD_CONTINUE SYM_SEMICOLON
SELECT -> KEYWD_SELECT SYM_B1 EXPR SYM_B2 SYM_P1 CASE_LIST SYM_P2
CASE_LIST -> KEYWD_CASE EXPR OP_TERN1 STMTS CASE_LIST
CASE_LIST -> KEYWD_DEFAULT OP_TERN1 STMTS
CASE_LIST -> ''
FUNCTION -> KEYWD_DEF IDENTIFIER SYM_B1 ARG_LIST SYM_B2 OP_FUNC TYPE SYM_P1 STMTS SYM_P2
ARG_LIST -> TYPE IDENTIFIER ARGNEXT
ARG_LIST -> ''
ARGNEXT -> ''
ARGNEXT -> OP_COMMA ARG_LIST
FUNCTION_CALL -> IDENTIFIER SYM_B1 PARAM_LIST SYM_B2
PARAM_LIST -> EXPR PARAM_LIST_NEXT
PARAM_LIST_NEXT -> ''
PARAM_LIST_NEXT -> OP_COMMA PARAM_LIST
TYPE -> D_TYPE TYPENEXT
TYPENEXT -> ''
TYPENEXT -> SYM_SB1 DIM SYM_SB2
DIM -> EXPR DIMNEXT
DIM -> ''
DIMNEXT -> OP_COMMA DIM
DIMNEXT -> ''
D_TYPE -> KEYWD_INT 
D_TYPE -> KEYWD_FLOAT
D_TYPE -> KEYWD_CHAR
D_TYPE -> KEYWD_BOOL
D_TYPE -> KEYWD_VOID 
D_TYPE -> KEYWD_STRING
EXPR -> SYM_B1 EXPR BIN_OP EXPR SYM_B2
EXPR -> PREFIX EXPR
EXPR -> FUNCTION_CALL
EXPR -> IDENTIFIER
EXPR -> LITERAL
EXPR -> SYM_P1 LIST SYM_P2
LISTNEXT -> OP_COMMA LIST
LISTNEXT -> ''
LIST -> EXPR LISTNEXT
LITERAL -> INT_LIT
LITERAL -> FLOAT_LITERAL
LITERAL -> CHAR_LIT
LITERAL -> BOOL_LIT
LITERAL -> STR_LIT
BOOL_LIT -> KEYWD_TRUE
BOOL_LIT -> KEYWD_FALSE
BIN_OP -> ARTM_OP
BIN_OP -> REL_OP
BIN_OP -> LOGCL_OP
BIN_OP -> BIT_OP
ARTM_OP -> OP_AR_add 
ARTM_OP -> OP_AR_sub 
ARTM_OP -> OP_AR_mul 
ARTM_OP -> OP_AR_div
ARTM_OP -> OP_AR_mod
REL_OP -> OP_REL_gt 
REL_OP -> OP_REL_ge 
REL_OP -> OP_REL_lt
REL_OP -> OP_REL_le 
REL_OP -> OP_REL_eq
REL_OP -> OP_REL_neq
LOGCL_OP -> OP_LOG_and
LOGCL_OP -> OP_LOG_or
LOGCL_OP -> OP_LOG_not
BIT_OP -> OP_BIT_and
BIT_OP -> OP_BIT_or
BIT_OP -> OP_BIT_ls
BIT_OP -> OP_BIT_rs
BIT_OP -> OP_BIT_xor
PREFIX -> OP_INCR
PREFIX -> OP_DECR
PREFIX -> OP_BIT4
PREFIX -> OP_AR_add
PREFIX-> OP_AR_sub